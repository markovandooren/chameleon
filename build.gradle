apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
//apply plugin: 'org.akhikhl.wuff.eclipse-ide-bundle'

sourceCompatibility = 1.8

repositories {
  mavenCentral()
  maven {
    url 'http://users.ugent.be/~mcvdoore/repository'
  }
}

configurations {
  plantuml
}
dependencies {
  plantuml 'net.sourceforge.plantuml:plantuml:8020'
}

def docSrc = 'build/tmp/src_for_doc'

sourceSets {
  main {
        java {
            srcDir 'src'
            exclude 'org/aikodi/chameleon/eclipse/**'
       }
  }
  dok {
        java {
            srcDir docSrc
            exclude 'org/aikodi/chameleon/eclipse/**'
       }
  }
}

dependencies {
  compile group: 'junit', name: 'junit', version: '4.+'
  compile group: 'com.google.guava', name: 'guava', version: '18.+'
  //compile project(':rejuse')
  compile group: 'markovandooren', name: 'rejuse', version: '1.3.0'
  compile group: 'org.antlr', name: 'antlr', version: '3.2'
  //compile "${eclipseMavenGroup}:org.eclipse.core.commands:+"
  
}

task javadoc(type: Javadoc, overwrite: true) {
    source = sourceSets.dok.java
}

ant.importBuild 'doc.xml'

task copyCode(type:Copy) {
  from sourceSets.main.java
  into docSrc
  include '**'
}

task copyDiagrams(type:Copy) {
  from docSrc
  into 'build/docs/javadoc'
  include '**/*.svg'
  filter { String line ->
    line.replaceAll(/(xlink:href=\S*)/, {it[0]+ / target="_parent"/})
  }
}



task runPlantUML(type: JavaExec) {
  main = 'net.sourceforge.plantuml.Run'
  classpath configurations.plantuml
  args '-tsvg', '-config', '"./plantuml.skin"', "${docSrc}/**"
}

task diagrams 

javadoc.dependsOn diagrams
javadoc.dependsOn prepareSourceForJavadoc
prepareSourceForJavadoc.dependsOn copyCode
diagrams.dependsOn copyDiagrams
copyDiagrams.dependsOn runPlantUML
runPlantUML.dependsOn copyCode

configurations {
  deployerJars
}

dependencies {
   deployerJars 'org.apache.maven.wagon:wagon-ssh:2.2'
}

uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.deployerJars
        repository(url: "scp://webssh.ugent.be/users/m/mcvdoore/WWW/repository/") {
            authentication(userName: "mcvdoore")
        }
        pom.version = '1.1.12'
        pom.groupId = 'markovandooren'
        pom.artifactId = 'chameleon'
    }
}
